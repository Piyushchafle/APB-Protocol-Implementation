class apb_driver extends uvm_driver #(apb_seq_item);
  `uvm_component_utils(apb_driver)

  virtual apb_if vif;

  function new(string name, uvm_component parent);
    super.new(name, parent);
  endfunction

  function void build_phase(uvm_phase phase);
    super.build_phase(phase);
    if (!uvm_config_db #(virtual apb_if)::get(this, "", "vif", vif))
      `uvm_fatal("DRV", "Virtual interface not found")
  endfunction

  task run_phase(uvm_phase phase);
    apb_seq_item tr;
    forever begin
      seq_item_port.get_next_item(tr);
      drive_transfer(tr);
      seq_item_port.item_done();
    end
  endtask

  task drive_transfer(apb_seq_item tr);
    // SETUP phase
    @(posedge vif.Pclk);
    vif.Pselx   <= 1;
    vif.Penable<= 0;
    vif.Pwrite <= tr.write;
    vif.Paddr  <= tr.addr;
    vif.Pwdata <= tr.data;

    // ACCESS phase
    @(posedge vif.Pclk);
    vif.Penable <= 1;

    // Wait for ready
    wait (vif.Pready == 1);

    // END transfer
    @(posedge vif.Pclk);
    vif.Pselx   <= 0;
    vif.Penable<= 0;
  endtask
endclass
