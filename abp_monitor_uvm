class apb_monitor extends uvm_monitor;
  `uvm_component_utils(apb_monitor)

  virtual apb_if vif;
  uvm_analysis_port #(apb_seq_item) mon_ap;

  function new(string name, uvm_component parent);
    super.new(name, parent);
    mon_ap = new("mon_ap", this);
  endfunction

  function void build_phase(uvm_phase phase);
    super.build_phase(phase);
    if (!uvm_config_db #(virtual apb_if)::get(this, "", "vif", vif))
      `uvm_fatal("MON", "Virtual interface not found")
  endfunction

  task run_phase(uvm_phase phase);
    apb_seq_item tr;
    forever begin
      @(posedge vif.Pclk);
      if (vif.Pselx && vif.Penable && vif.Pready) begin
        tr = apb_seq_item::type_id::create("tr");
        tr.write = vif.Pwrite;
        tr.addr  = vif.Paddr;
        tr.data  = vif.Pwrite ? vif.Pwdata : vif.Prdata;
        mon_ap.write(tr);
      end
    end
  endtask
endclass
